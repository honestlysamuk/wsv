//! A parser for a WSV file

// I want to ignore the surrounding quotation marks on strings within the grammar rules, rather than manually remove them later in the parsing. This means that whenever there are no double quotes or new lines, I don't need to modify the input, which I suspect will be most of the time

WHITESPACE  = _{ (!"\n" ~ WHITE_SPACE) }
NewLine     =  { "\"/\"" }
DoubleQuote =  { "\"\"" }
StringPart  =  { (!("\n" | "\"") ~ ANY)+ }

Null    =  { "-" }
Value   = @{ (!("\n" | "\"" | "#" | WHITESPACE) ~ ANY)+ }
String  = ${ ("\"" ~ (DoubleQuote | NewLine | StringPart)* ~ "\"") }
Comment = _{ "#" ~ (!"\n" ~ ANY)* ~ &("\n" | EOI) }

Line =  { (Null | String | Value)* ~ Comment? }
Wsv  =  { SOI ~ Line ~ ("\n" ~ Line)* ~ EOI }